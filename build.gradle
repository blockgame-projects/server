plugins {
    id 'java'
    id 'application'
    id("io.freefair.lombok") version "8.13.1"
}

// Creates the SHA Commit Hash
def gitSha = { ->
    try {
        def p = "git rev-parse --short HEAD".execute()
        p.waitFor()
        return p.exitValue() == 0 ? p.in.text.trim() : "unknown"
    } catch (ignored) {
        return "unknown"
    }
}()

// Check if we are in dev mode
def isDev = (project.findProperty("dev") ?: "true").toString().toBoolean()

group = 'com.james090500'
version = isDev ? "${project.version}-${gitSha}" : "${project.version}"

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
}


repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    implementation 'io.netty:netty-all:4.2.5.Final'
    implementation "org.joml:joml:1.10.8"
    implementation 'org.lz4:lz4-java:1.8.0'
}

// Builds the JAr
tasks.withType(Jar).configureEach {
    archiveBaseName.set(project.findProperty("archivesBaseName") ?: project.name)
    archiveVersion.set(project.version) // already includes -<sha> when dev=true
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

/** Tiny helper tasks for CI */
tasks.register("printVersion") {
    doLast { println project.version }
}
tasks.register("printIsDev") {
    doLast { println isDev ? "true" : "false" }
}